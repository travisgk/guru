#version 330 core
#define N_DIR_LIGHTS 1
#define N_POINT_LIGHTS 1

struct DirLight {
	vec3 diffuse;
	vec3 specular;
};

struct PointLight {
	vec3 diffuse;
	vec3 specular;
	float constant;
	float linear;
	float quadratic;
};

in Shared {
	vec2 tex_coords;
	vec3 tangent_view_pos;
	vec3 tangent_frag_pos;
	vec3 tangent_view_frag_diff;
	vec3 tangent_dir_light_raw_dirs[N_DIR_LIGHTS];
	vec3 tangent_point_light_pos[N_POINT_LIGHTS];
	vec3 tangent_point_light_raw_dirs[N_POINT_LIGHTS];
} fs_in;

out vec4 FragColor;

uniform vec3 _ambient_color;
uniform sampler2D _diffuse_texture_ID;
uniform sampler2D _normal_texture_ID;
uniform sampler2D _metallic_texture_ID;
uniform sampler2D _roughness_texture_ID;
uniform DirLight _dir_lights[N_DIR_LIGHTS];
uniform PointLight _point_lights[N_POINT_LIGHTS];

void main() {
	vec3 normal = texture(_normal_texture_ID, fs_in.tex_coords).rgb;
	normal = normalize(normal * 2.0 - 1.0);
	
	// gets diffuse color. transparency not used at this moment.
	vec3 diff_rgb = texture(_diffuse_texture_ID, fs_in.tex_coords).rgb;
	float spec_strength = texture(_metallic_texture_ID, fs_in.tex_coords).r;
	float roughness = (1.0 - texture(_roughness_texture_ID, fs_in.tex_coords).r) * 255.0 + 1.0;
	
	vec3 rgb_result = _ambient_color * diff_rgb;
	
	vec3 tangent_view_dir = normalize(fs_in.tangent_view_frag_diff);
	for (int i = 0; i < N_DIR_LIGHTS; ++i) {
		// diffuse
		vec3 light_dir = normalize(fs_in.tangent_dir_light_raw_dirs[i]);
		float diff = max(dot(light_dir, normal), 0.0);
		vec3 diffuse = _dir_lights[i].diffuse * diff * diff_rgb;

		// specular
		vec3 halfway_dir = normalize(light_dir + tangent_view_dir);
		float spec = pow(max(dot(normal, halfway_dir), 0.0), roughness);
		vec3 specular = _dir_lights[i].specular * spec_strength * spec;
		rgb_result += diffuse + specular;
	}
	
	for (int i = 0; i < N_POINT_LIGHTS; ++i) {
		vec3 light_dir = normalize(fs_in.tangent_point_light_raw_dirs[i]);
	
		// diffuse
		float diff = max(dot(light_dir, normal), 0.0);
		vec3 diffuse = _point_lights[i].diffuse * diff * diff_rgb;
		
		// specular
		vec3 halfway_dir = normalize(light_dir + tangent_view_dir);
		float spec = pow(max(dot(normal, halfway_dir), 0.0), roughness);
		vec3 specular = _point_lights[i].specular * spec_strength * spec;
		
		// attenuation
		float distance = length(fs_in.tangent_point_light_raw_dirs[i]);
		float attenuation = 1.0 / (
			  _point_lights[i].constant 
			+ _point_lights[i].linear * distance 
			+ _point_lights[i].quadratic * (distance * distance)
		);
		
		rgb_result += diffuse * attenuation + specular * attenuation;
	}
	
	FragColor = vec4(rgb_result, 1.0);
}